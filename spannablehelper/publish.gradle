apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'
group 'com.dacui.util'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/spannablehelper-release.aar")
            groupId this.group
            artifactId 'example'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(file(project.rootProject.file('signinfo.properties')).newDataInputStream())

    user = properties.getProperty("BINTRAY_USER")
    key = properties.getProperty("BINTRAY_API_KEY")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'SpannableHelper'
        description = "Easy use android SpannableString"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        dryRun = true
        version {
            name = this.version
            desc = "Example ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}